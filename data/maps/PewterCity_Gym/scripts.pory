raw `
PewterCity_Gym_MapScripts::
	.byte 0

PewterCity_Gym_EventScript_Brock::
	famechecker FAMECHECKER_BROCK, FCPICKSTATE_COLORED, UpdatePickStateFromSpecialVar8005
	setvar VAR_TEMP_START_EVENT_BATTLE, 2
	trainerbattle_single TRAINER_LEADER_BROCK, PewterCity_Gym_Text_BrockIntro, PewterCity_Gym_Text_BrockDefeat, PewterCity_Gym_EventScript_DefeatedBrock, NO_MUSIC
	goto_if_unset FLAG_GOT_TM39_FROM_BROCK, PewterCity_Gym_EventScript_GiveTM39
	msgbox PewterCity_Gym_Text_BrockPostBattle
	release
	end

PewterCity_Gym_EventScript_DefeatedBrock::
	famechecker FAMECHECKER_BROCK, 1
	setflag FLAG_DEFEATED_BROCK
	setflag FLAG_BADGE01_GET
	setvar VAR_MAP_SCENE_PEWTER_CITY, 1
	setflag FLAG_HIDE_PEWTER_CITY_GYM_GUIDE
	clearflag FLAG_HIDE_PEWTER_CITY_RUNNING_SHOES_GUY
	set_gym_trainers 1
	goto PewterCity_Gym_EventScript_GiveTM39
	end

PewterCity_Gym_EventScript_GiveTM39::
	msgbox PewterCity_Gym_Text_TakeThisWithYou
	checkitemspace ITEM_TM39
	goto_if_eq VAR_RESULT, FALSE, PewterCity_Gym_EventScript_NoRoomForTM39
	giveitem_msg PewterCity_Gym_Text_ReceivedTM39FromBrock, ITEM_TM39
	setflag FLAG_GOT_TM39_FROM_BROCK
	msgbox PewterCity_Gym_Text_ExplainTM39
	closemessage
	setweather WEATHER_RAIN
	doweather
	delay 130
	msgbox PewterCity_Gym_Text_Sprinklers
	closemessage
	delay 50
	msgbox PewterCity_Gym_Text_DryingPan
	release
	end

PewterCity_Gym_EventScript_NoRoomForTM39::
	msgbox PewterCity_Gym_Text_DontHaveRoomForThis
	release
	end

PewterCity_Gym_EventScript_Liam::
	trainerbattle_single TRAINER_CAMPER_LIAM, PewterCity_Gym_Text_LiamIntro, PewterCity_Gym_Text_LiamDefeat
	msgbox PewterCity_Gym_Text_LiamPostBattle, MSGBOX_AUTOCLOSE
	end

PewterCity_Gym_EventScript_GymGuy::
	lock
	faceplayer
	goto_if_set FLAG_DEFEATED_BROCK, PewterCity_Gym_EventScript_GymGuyPostVictory
	msgbox PewterCity_Gym_Text_LetMeTakeYouToTheTop, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, PewterCity_Gym_EventScript_GymGuyTakeMeToTop
	goto_if_eq VAR_RESULT, NO, PewterCity_Gym_EventScript_GymGuyDontTakeMeToTop
	end

PewterCity_Gym_EventScript_GymGuyPostVictory::
	msgbox PewterCity_Gym_Text_YoureChampMaterial
	release
	end

PewterCity_Gym_EventScript_GymGuyTakeMeToTop::
	msgbox PewterCity_Gym_Text_LetsGetHappening
	goto PewterCity_Gym_EventScript_GymGuyAdvice
	end

PewterCity_Gym_EventScript_GymGuyDontTakeMeToTop::
	msgbox PewterCity_Gym_Text_ItsFreeLetsGetHappening
	goto PewterCity_Gym_EventScript_GymGuyAdvice
	end

PewterCity_Gym_EventScript_GymGuyAdvice::
	msgbox PewterCity_Gym_Text_TryDifferentPartyOrders
	release
	end

PewterCity_Gym_EventScript_GymStatue::
	lockall
	goto_if_set FLAG_BADGE01_GET, PewterCity_Gym_EventScript_GymStatuePostVictory
	msgbox PewterCity_Gym_Text_GymStatue
	releaseall
	end

PewterCity_Gym_EventScript_GymStatuePostVictory::
	msgbox PewterCity_Gym_Text_GymStatuePlayerWon
	releaseall
	end
`
script PewterCity_EventScript_Dad {
	lock
	faceplayer
	textcolor(0)
	// switch statement about the weather
	if (flag(FLAG_GOT_TM39_FROM_BROCK)) {
		msgbox("DAD: {PLAYER}!\nWhat are you doing out here?\pNo, seriously.\nWe're REALLY far from home.\lHow'd you manage the hike?\pWell, never mind that.\nHow about you take a rest?", MSGBOX_DEFAULT)
	} else {
			msgbox("DAD: {PLAYER}!\nWhat are you doing out here?\pHmm, the sprinklers in here are\nreally bothersome.\lI should've brought an umbrella!\pWell, never mind that.\nHow about you take a rest?", MSGBOX_DEFAULT)
	}

	call(EventScript_DadHeal)
	release
	end
}

script PewterCity_EventScript_LightYearsSet {
	setvar(VAR_TEMP_0, 0)
	// setvar(VAR_CSR_WentToBrock, 1)
	//For the Celia check if you leave
	release
}

script PewterCity_EventScript_LightYearsGo {
	specialvar(VAR_FACING, GetPlayerFacingDirection)
	if(var(VAR_FACING) == DIR_NORTH) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp2)
	} elif(var(VAR_FACING) == DIR_SOUTH) {
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown2)
	}
	fadescreen(FADE_TO_BLACK)
	waitmovement(0)
	lock
	msgbox("58,786,253,730,000,000 miles\nlaterâ€¦", MSGBOX_DEFAULT)
	closemessage
		if(var(VAR_FACING) == DIR_NORTH) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp2)
	} elif(var(VAR_FACING) == DIR_SOUTH) {
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown2)
	}
	fadescreen(FADE_FROM_BLACK)
	waitmovement(0)
	switch(var(VAR_TEMP_0)) {
		case 0:
			setvar(VAR_TEMP_0, 1)
		case 1:
			setvar(VAR_TEMP_0, 0)
	}
	if(flag(FLAG_DEFEATED_BROCK)) {
		clearflag(FLAG_CSR_ABSCONDED_BROCK)
	} else {
		setflag(FLAG_CSR_ABSCONDED_BROCK)
	}
	release
}

script PewterCity_EventScript_CeliaDebug {
	msgbox("In the decomp update, I improved\nthe LightYears cutscene slightly.\pI now have the player continue to\nwalk while the screen fades.\lBefore, they'd just abruptly stop.\pDebugging that stupid code took me\nmore than three hours.\lI hate this.", MSGBOX_NPC)
	end
}

script PewterCity_Gym_EventScript_NoSneaky {
	checktrainerflag(TRAINER_CAMPER_LIAM)
	if(var(VAR_RESULT) == FALSE) {
		lock
		playbgm(MUS_ENCOUNTER_BOY, FALSE)
		applymovement(2, Common_Movement_ExclamationMark)
		waitmovement(0)
		delay(75)
		applymovement(2, BoyWalksUp)
		waitmovement(0)
		textcolor(0)
		msgbox(PewterCity_Gym_Text_LiamIntro, MSGBOX_DEFAULT)
		trainerbattle_no_intro(TRAINER_CAMPER_LIAM, PewterCity_Gym_Text_LiamDefeat)
	}
}

movement BoyWalksUp {
	walk_right * 3
	walk_up * 2
	walk_left * 3
}