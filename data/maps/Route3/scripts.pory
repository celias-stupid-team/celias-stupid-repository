raw `
Route3_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION Route3_TransitionScripts
	.byte 0

Route3_EventScript_Youngster::
	msgbox Route3_Text_TunnelFromCeruleanTiring, MSGBOX_NPC
	end

Route3_EventScript_RouteSign::
	msgbox Route3_Text_RouteSign, MSGBOX_SIGN
	end
`
script Route3_TransitionScripts {
	setbench(BENCH_PEWTER)
	call(SetPlayerPokedexValues)
}
script Route3_EventScript_Lass_North {
	lock
	setvar(VAR_TEMP_1,5)
	goto(Route3_EventScript_Lass)
}

script Route3_EventScript_Lass_South {
	lock
	setvar(VAR_TEMP_1,4)
	goto(Route3_EventScript_Lass)
}

script Route3_EventScript_Lass {
	setvar(VAR_MAP_SCENE_CSR_LASS_1, 1)
	playbgm(MUS_ENCOUNTER_GIRL, 1)
	delay(90)
	 //set Rumor Log variables 4,3
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_QuestionMark)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Lass_Player_Start)
	waitmovement(0)
	switch(var(VAR_TEMP_1)) {
		case 5:
			applymovement(5,Lass_Attack_North)
		case 4:
			applymovement(5,Lass_Attack_South)
	}
	waitmovement(0)
	delay(10)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Lass_Player_End)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(10)
	textcolor(NPC_TEXT_COLOR_FEMALE)
	msgbox(Route3_Text_JaniceIntro)
	fadedefaultbgm
	trainerbattle_no_intro(TRAINER_LASS_JANICE,Route3_Text_JaniceDefeat)
	release
	end
}

script Route4_EventScript_Dad {
	lock
	faceplayer
	textcolor(0)
	msgbox("DAD: {PLAYER}!\nWhat are you doing out here?\pDid you get the RUNNING SHOES\nI sent to you?\lYou'd better be wearing them!\pWell, never mind that.\nHow about you take a rest?", MSGBOX_DEFAULT)
	call(EventScript_DadHeal)
	release
	end
}

script Route4_Trigger_Lass2 {
	lock
	setvar(VAR_MAP_SCENE_CSR_LASS_2, 1)
	getplayerxy(VAR_TEMP_4, VAR_TEMP_5) //Find the spot 8 over from the player; place the Lass there
	addvar(VAR_TEMP_4, 8) 
	addobject(3)
	setobjectxy(3, VAR_TEMP_4, 8)
	//setobjectxyperm(3, VAR_TEMP_4, 8)
	release
}

script Route4_EventScript_Hbox {
	lock
	playmoncry(SPECIES_JIGGLYPUFF, CRY_MODE_ENCOUNTER)
	waitmoncry
	setflag(FLAG_CSR_HBOX_TEMP_DISAPPEARANCE)
	setwildbattle(SPECIES_JIGGLYPUFF, 13, ITEM_SODA_POP)
	dowildbattle
	
	fadescreenspeed(FADE_TO_BLACK, 1)
	addobject(9)
	setobjectxy(9, 35, 8)
	setmetatile(35, 14, 0x2C7, FALSE)
	setmetatile(35, 13, 0x2C7, FALSE)
	setmetatile(35, 12, 0x2C7, FALSE)
	setmetatile(35, 11, 0x2C7, FALSE)
	fadescreenspeed(FADE_FROM_BLACK, 1)

	delay(30)
	playse(SE_FALL)
	applymovement(9, Common_Movement_Descend)
	waitmovement(0)
	playse(SE_BANG)
	setvar(VAR_0x8006, 5)
	setvar(VAR_0x8007, 3)
	special(ShakeScreen)
	delay(20)
	setmetatile(35, 14, 0x00D, FALSE)
	setmetatile(35, 13, 0x00D, FALSE)
	setmetatile(35, 12, 0x00D, FALSE)
	setmetatile(35, 11, 0x00D, FALSE)
	release
}

script Route3_Item_Pokeball {
	finditem(ITEM_POKE_BALL)
}

script Route3_Far_SetDex {
	setvar(VAR_TEMP_0, 1)
	setflag(FLAG_CSR_MAP_ROUTE_3_FAR)
	call(SetPlayerPokedexValues)
	release
}

script Route3_EventScript_Celia_Wurmple {
	msgbox("It took me WAY too long to give\nthis guy a Wurmple.\lIdk why.", MSGBOX_NPC)
	end
}

script Route3_EventScript_Celia_Lass2 {
	msgbox("In the Beta, the LASS that attacks\nyou here would randomly teleport.\pI could never figure out why or how,\nso I just ignored it in the demo.\lNow she works much better.\pBy the way! You can walk through\nthe wall here!\pI'm sure the Celia's Stupid Romhack\nspeedrunners will appreciate that.", MSGBOX_NPC)
	end
}

script Route3_EventScript_Celia_PuffSign {
	msgbox("I couldn't decide if I should write\nthis Route as 1 or 2 new mons.\pWhichever way I chose, it would\nconfuse people cuz Puff was static.\pThus, I decided to make a joke\nout of it to make sure it was clear!", MSGBOX_NPC)
	end
}

movement Lass_Player_Start {
	
	delay_16 * 5
	face_left
	delay_16 * 2
	delay_8
	face_right
	delay_1 * 30
	face_left
}
movement Lass_Attack_North {
	walk_left * 4
	walk_up * 5
	walk_left * 5
}
movement Lass_Attack_South {
	walk_left * 4
	walk_up * 4
	walk_left * 5
}


movement Lass_Player_End {
	
	face_right
}

script Route3_DugrtioSign {
	lock
	msgbox("There's a mysterious carving in the\nmountainside.\lIt seems to resemble a POKéMON...", MSGBOX_DEFAULT)
	if(!flag(FLAG_DUGTRIO_SIGN)) {
		closemessage
		delay(10)
		playfanfare(MUS_SLOTS_WIN)
		msgbox("{PLAYER} discovered a\nPOKéMON SIGN!", MSGBOX_DEFAULT)
		waitfanfare
		delay(10)
		setflag(FLAG_DUGTRIO_SIGN)
	}
	release
}

script Route3_Nerd {
	msgbox("I dropped my glasses...\nCan you help me find them?", MSGBOX_NPC)
	end
}

script Route3_Celia_Signs {
	msgbox("My favorite part of Pokemon Snap as\na kid was going back through the\lcourses and finding the signs.\pI really wanted to recreate that\nsense of discovery here.", MSGBOX_NPC)
	end
}

script Route3_Celia_Glasses {
	msgbox("This whole area was added to be a\nred herring for SURGE.\pI knew the player would obviously\njump to BLACKGLASSES as the answer,\lso I needed to address it somehow.\pThus, I made a stupid scene where I\ndangle them in front of you but make\lCLEAR that you can't get them.", MSGBOX_NPC)
	end
}

script Route3_ShortsBerry {
	msgbox("Hey, is it just me or are POTIONS\nreally hard to find as of late?\pThat's okay, though.\nI know a secret!\pJust look for darker spots on the\nground.\lYou'll find BERRIES growing there!\pThankfully I'm SHORTS, so my\npockets can carry plenty!", MSGBOX_NPC)
	end
}

const Nerd = 15
script Route3_Blackglasses {
	lock
	if(flag(FLAG_CSR_GLASSES_NERD)) {
		msgbox("There is no longer a BLACKGLASSES\nhere.\pGuess you're out of luck...\n", MSGBOX_DEFAULT)
		release
	} else {
		playfanfare(MUS_OBTAIN_ITEM)
		msgbox("{PLAYER} found one, singular\nBLACKGLASSES!", MSGBOX_DEFAULT)
		waitfanfare
		msgbox("{PLAYER} put the BLACKGLASSES in\nthe ITEMS pocket.", MSGBOX_DEFAULT)
		closemessage
		delay(20)
		playse(SE_PIN)
		applymovement(Nerd, NerdLooksOver)
		waitmovement(0)
		waitse 
		delay(30)
		textcolor(NPC_TEXT_COLOR_MALE)
		msgbox("Hey!\n", MSGBOX_DEFAULT)
		closemessage
		delay(15)
		copyvar(VAR_TEMP_1, VAR_FACING)
		switch(var(VAR_TEMP_1)) {
			case DIR_WEST: //3
				applymovement(Nerd,NerdApproachesPlayerRight)
				waitmovement(0)
				delay(5)
				applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
				waitmovement(0)
				break
			case DIR_SOUTH:
				applymovement(Nerd,NerdApproachesPlayerFar)
				waitmovement(0)
				delay(5)
				applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
				waitmovement(0)
				break
			case DIR_EAST:
				applymovement(Nerd,NerdApproachesPlayerFar)
				waitmovement(0)
				delay(5)
				break

		}
		playse(SE_SELECT)
		msgbox("These are my GLASSES!\nThanks so much for finding them!\p... ... ...\pUh, why'd you put them in your\nBAG?\lAren't you gonna give them back?", MSGBOX_DEFAULT)
		closemessage
		delay(20)
		switch(var(VAR_TEMP_1)) {// 1 
			case DIR_SOUTH:
				applymovement(OBJ_EVENT_ID_PLAYER, PlayerIsShook)
				waitmovement(0)
				
				applymovement(Nerd, NerdIsSadUp)
				waitmovement(0)
				applymovement(OBJ_EVENT_ID_PLAYER,Common_Movement_FaceDown)
				delay(10)
				break
			case DIR_WEST:
				applymovement(OBJ_EVENT_ID_PLAYER, PlayerIsShook)
				waitmovement(0)
				applymovement(Nerd, NerdIsSad)
				waitmovement(0)
				delay(10)
				break

			case DIR_EAST:
				applymovement(OBJ_EVENT_ID_PLAYER, PlayerIsShookInverse)
				waitmovement(0)
				applymovement(Nerd, NerdIsSad)
				waitmovement(0)
				delay(10)
				break
		
		}
		
		playse(SE_SELECT)
		msgbox("C'mon, man...\nThat's not cool.", MSGBOX_DEFAULT)
		closemessage
		delay(20)
		textcolor(NPC_TEXT_COLOR_NEUTRAL)
		playse(SE_SELECT)
		msgbox("{PLAYER} reluctantly returned the\nBLACKGLASSES to the dumb NERD…", MSGBOX_DEFAULT)
		closemessage
		delay(45)
		textcolor(NPC_TEXT_COLOR_MALE)
		playse(SE_SELECT)
		msgbox("Uh, thanks.\p...\pGoodbye.\n", MSGBOX_DEFAULT)
		closemessage
		playse(SE_EXIT)
		fadescreen(FADE_TO_BLACK)
		removeobject(Nerd)
		fadescreen(FADE_FROM_BLACK)
		release
	}
}

movement NerdLooksOver {
	face_player
	emote_exclamation_mark
}

movement PlayerIsShook {
	face_left
	delay_4 * 5
	face_right
	delay_4 * 5
	face_left
	delay_4 * 7
	face_right
	delay_4 * 5
}

movement PlayerIsShookInverse {
	
	face_right
	delay_4 * 5
	face_left
	delay_4 * 7
	face_right
	delay_4 * 5
	face_left
	delay_4 * 5
	face_right
	delay_4 * 5
}

movement NerdIsSad {
	walk_in_place_left * 3
}

movement NerdIsSadUp {
	walk_in_place_Up * 3
}

movement NerdApproachesPlayerRight {
	walk_left *2
	face_player
}

movement NerdApproachesPlayerFar {
	walk_left *4
	face_player
}