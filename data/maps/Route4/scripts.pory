raw `
Route4_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION Route4_TransitionScripts
	.byte 0

Route4_EventScript_Unused::
	end

Route4_EventScript_Woman::
	msgbox Route4_Text_TrippedOverGeodude, MSGBOX_NPC
	end

Route4_EventScript_MtMoonSign::
	msgbox Route4_Text_MtMoonEntrance, MSGBOX_SIGN
	end

Route4_EventScript_RouteSign::
	msgbox Route4_Text_RouteSign, MSGBOX_SIGN
	end
`
const CAMPER = 14
script Route4_TransitionScripts {
	setbench(BENCH_ROUTE4_EAST)
	call(SetPlayerPokedexValues)
	if(!flag(FLAG_DEFEATED_MISTY)) {
		removeobject(CAMPER)
	} else {
		addobject(CAMPER)
	}
}

script Route4_SetFarBench {
	setbench(BENCH_ROUTE4)
	release
}

script Route4_SetCeruleanBench {
	setbench(BENCH_CERULEAN)
	release
}

script Route4_Item_PokeBall {
	finditem(ITEM_POKE_BALL)
}

script Route4_EventScript_Punch {
	lock
	textcolor(NPC_TEXT_COLOR_NEUTRAL)
	msgbox("A man is lying dead on the ground.\pHe appears to have been kicked\nvery hard…", MSGBOX_DEFAULT)
	release
}

script Route4_EventScript_Kick {
	lock
	textcolor(NPC_TEXT_COLOR_NEUTRAL)
	msgbox("The body of a man is lying here.\pIt appears he was punched to\ndeath…", MSGBOX_DEFAULT)
	release
}

script Route4_EventScript_CandyGirl {
	msgbox("Have you found any pieces of\nCANDY laying on the ground?\pI think there's one in every\nmajor area.\lI wonder where they came from!", MSGBOX_NPC)
	end
}

script Route4_EventScript_MinusSitrusBerries {
	lock
	if(flag(FLAG_CSR_ROUTE_4_BERRIES)) {
		msgbox("After placing two SITRUS BERRIES\nhere, there is nothing left.", MSGBOX_DEFAULT)
	} else {
		checkitem(ITEM_SITRUS_BERRY, 2)
		if(var(VAR_RESULT)) {
			playfanfare(MUS_TOO_BAD)
			msgbox("{PLAYER} found -2 SITRUS BERRIES!\n", MSGBOX_AUTOCLOSE)
			waitfanfare
			msgbox("{PLAYER} took the SITRUS BERRIES\nout of the BERRY POUCH.", MSGBOX_DEFAULT)
			removeitem(ITEM_SITRUS_BERRY, 2)
			setflag(FLAG_CSR_ROUTE_4_BERRIES)
		} else {
			msgbox("There's nothing here.\pOr wait, maybe there's even\nless than that...", MSGBOX_DEFAULT)
		}
	}
	release
}

script Route4_Celia_TMGuy {
	msgbox("For a while, this guy directly\nsaid “Use a TM!”\pI didn't like that cuz it made the\nsolution WAY too obvious.\lBut without him, people got stuck.", MSGBOX_NPC)
	end
}

script Route4_Celia_MtMoon {
	msgbox("Ah, MT. MOON.\pBy far the single hardest part of\nthis hack to get right for me.\pAnd also, the sole reason why I\nwill never try putting a dungeon in\lthis hack again!\pMaking the tentacools sufficiently\nannoying but not infuriating was\lone hell of a balancing act.\pPlus, the fight with the boss at the\nend was so hard to design!\pI'm pretty happy with how it landed\nin the end, but it took a lot of\lwork to get here.", MSGBOX_NPC)
	end
}

script Route4_Celia_Berries {
	msgbox("This bit is, like, the pure essence\nof this hack.\pIt /qohurts/qc you, but it's not\nlike you actually ever used the\lSitrus Berries anyway.\pSo it's only really mean if you\nlook at the principle of it.\lThe player isn't actually hurt.", MSGBOX_NPC)
	end
}

script Route4_Celia_Picknicker {
	msgbox("That trainer there is a LASS in\nvanilla.\pI needed to swap her out for a\nPICKNICKER so it wouldn't mess\lwith my Lass running gag.", MSGBOX_NPC)
	end
}

script Route4_DejectedCamper {
	msgbox("Oh, darn it!\pI just made it through MT. MOON,\nbut I left my bag at home!\pGuess it's time to walk all the way\nback around KANTO…", MSGBOX_NPC)
	end
}
